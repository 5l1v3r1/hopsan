include( ../Common.prf )

#Helpfunction to fetch correct FMILibrary INCLUDEPATH and LIBS path
defineReplace(setFMILIBPathInfo){
    #Assign input arguments
    externalSrc = $$1
    dstDir = $$2

    #Set FMILibrary paths, Paths that are higher up in the list will have priority if found
    FMILIB_PATHS *= $${PWD}/../Dependencies/FMILibrary-2.0.1
    win32:contains(QMAKE_HOST.arch, x86_64){
        # Fallback OBSOLETE 64 bit Windows release
        FMILIB_PATHS *= $${PWD}/../Dependencies/FMILibrary-2.0.1_x64
    }
    FMILIB_PATH = $$selectPath($$externalSrc, $$FMILIB_PATHS, "FMILibrary")

    #warning(FMILIB_PATH $${FMILIB_PATH})

    #Empty variables to fill in
    magic_hopsan_libpath =
    magic_hopsan_includepath =
    magic_hopsan_qmake_post_link =
    libDir =

    exists($${FMILIB_PATH}){
        libDir = $${FMILIB_PATH}
        #Check libfolder path
        exists($${libDir}){
            files =

            win32:magic_hopsan_libpath = -L$${libDir}/install/lib -llibfmilib_shared
            unix:magic_hopsan_libpath = -L$${libDir}/install/lib -lfmilib_shared
            magic_hopsan_includepath *= $${FMILIB_PATH}/install/include
            magic_hopsan_includepath *= $${FMILIB_PATH}

            #Set files to copy
            win32:files = install/lib/libfmilib_shared$${DLL_EXT}*
            unix:files = install/lib/libfmilib_shared$${DLL_EXT}*

            #Generate the copy command
            magic_hopsan_qmake_post_link = $$generateCopyDllCommand($$files, $$libDir, $$dstDir)
            #message(qmake_post_link $${magic_hopsan_qmake_post_link})
        }
    }

    !exists($${libDir}){
        !build_pass:warning("Could not find your compiled FMILibrary dll")
        magic_hopsan_libpath =
        magic_hopsan_includepath =
        magic_hopsan_qmake_post_link =
    }

    #Export lists to INCLUDEPATH, LIBS and QMAKE_POST_LINK variables
    export(magic_hopsan_libpath)
    export(magic_hopsan_includepath)
    export(magic_hopsan_qmake_post_link)

    #message(----------------Includepath is $${magic_hopsan_includepath})
    #message(----------------Libs is $${magic_hopsan_libpath})

    return($$magic_hopsan_libpath)
}
