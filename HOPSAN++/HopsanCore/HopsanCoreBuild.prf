include( ../Common.prf )

#Helpfunction to fetch correct TBB INCLUDEPATH and magic_hopsan_libpath path
defineReplace(setTBBWindowsPathInfo){
    #Assign input arguments
    externalSrc = $$1
    dstDir = $$2

    #Set default tbb path alternatives, higher up is prefered
    contains($$QMAKE_HOST.arch, x86_64){
        #64 bit paths
        TBB_PATHS *= $${PWD}/Dependencies/tbb41_20130613oss_64
        TBB_PATHS *= $${PWD}/Dependencies/tbb40_20111003oss_64
    } else {
        #32 bit paths
        TBB_PATHS *= $${PWD}/Dependencies/tbb41_20130613oss
        TBB_PATHS *= $${PWD}/Dependencies/tbb40_20111003oss
    }
    #Try environment variable first $$(ENVVARNAME)if it exists, then default paths listed above
    TBB_PATH = $$selectPath($$externalSrc, $$TBB_PATHS, "tbb")

    #Empty variables to fill in
    magic_hopsan_libpath =
    magic_hopsan_includepath =
    magic_hopsan_qmake_post_link =
    files =
    libDir =

    # Check for output dir (must have been specified when building tbb)
    exists($${TBB_PATH}/build/output_release){
        CONFIG(debug, debug|release) {
            libDir = $${TBB_PATH}/build/output_debug
            magic_hopsan_libpath *= -L$${libDir} -ltbb_debug
            files = tbb_debug.dll
        }
        CONFIG(release, debug|release) {
            libDir = $${TBB_PATH}/build/output_release
            magic_hopsan_libpath *= -L$${libDir} -ltbb
            files = tbb.dll
        }
        magic_hopsan_includepath *= $${TBB_PATH}/include/tbb
        magic_hopsan_qmake_post_link += $$generateCopyDllCommand($$files, $$libDir, $$dstDir)
    }

    # Check hardcoded libfolder path works for 32 bit tbb40_20111003oss
    exists($${TBB_PATH}/build/windows_ia32_gcc_mingw4.4.0_release){
        CONFIG(debug, debug|release) {
            libDir = $${TBB_PATH}/build/windows_ia32_gcc_mingw4.4.0_debug
            magic_hopsan_libpath *= -L$${libDir} -ltbb_debug
            files = tbb_debug.dll
        }
        CONFIG(release, debug|release) {
            libDir = $${TBB_PATH}/build/windows_ia32_gcc_mingw4.4.0_release
            magic_hopsan_libpath *= -L$${libDir} -ltbb
            files = tbb.dll
        }
        magic_hopsan_includepath *= $${TBB_PATH}/include/tbb
        magic_hopsan_qmake_post_link += $$generateCopyDllCommand($$files, $$libDir, $$dstDir)
    }

    #message(magic_hopsan_qmake_post_link $${magic_hopsan_qmake_post_link})

    !exists($${libDir}){
        warning(Could not find your compiled TBB dll)
        magic_hopsan_libpath =
        magic_hopsan_includepath =
        magic_hopsan_qmake_post_link =
    }

    #Export lists to INCLUDEPATH, LIBS and QMAKE_POST_LINK variables
    export(magic_hopsan_libpath)
    export(magic_hopsan_includepath)
    export(magic_hopsan_qmake_post_link)

    #message(----------------Includepath is $${magic_hopsan_includepath})
    #message(----------------magic_hopsan_libpath is $${magic_hopsan_libpath})
    return($$magic_hopsan_libpath)
}
