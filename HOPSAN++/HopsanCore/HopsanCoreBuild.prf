include( ../Common.prf )

#Helpfunction to fetch correct TBB INCLUDEPATH and LIBS path
defineReplace(setWindowsTBBPathsAndCopyDll){
    #Assign input arguments
    externalSrc = $$1
    dstDir = $$2

    #Set default tbb path alternatives, higher up is prefered
    TBB_PATHS *= $${PWD}/../ExternalDependencies/tbb30_20110315oss
    TBB_PATHS *= $${PWD}/../ExternalDependencies/tbb30_20101215oss
    TBB_PATHS *= $${PWD}/../ExternalDependencies/tbb30_20100915oss
    TBB_PATHS *= $${PWD}/../ExternalDependencies/tbb30_20100406oss
    #Try environment variable first $$(ENVVARNAME)if it exists, then default paths listed above
    TBB_PATH = $$selectPath($$externalSrc, $$TBB_PATHS, "tbb")

    #Empty variables to fill in
    libs =
    includes =
    qmake_post_link =
    files =
    libDir =

    #Check newest libfolder path
    exists($${TBB_PATH}/build/windows_ia32_gcc_mingw4.4.0_release){
        CONFIG(debug, debug|release) {
            libDir = $${TBB_PATH}/build/windows_ia32_gcc_mingw4.4.0_debug
            libs *= -L$${libDir} -ltbb_debug
            files = tbb_debug.dll
        }
        CONFIG(release, debug|release) {
            libDir = $${TBB_PATH}/build/windows_ia32_gcc_mingw4.4.0_release
            libs *= -L$${libDir} -ltbb
            files = tbb.dll
        }
        includes *= $${TBB_PATH}/include/tbb
        qmake_post_link += $$generateCopyDllCommand($$files, $$libDir, $$dstDir)
    }

    #Check old style libfolder path, remove when we force people to upgrade
    exists($${TBB_PATH}/build/windows_ia32_gcc_mingw_release){
        CONFIG(debug, debug|release) {
            libDir = $${TBB_PATH}/build/windows_ia32_gcc_mingw_debug
            libs *= -L$${libDir} -ltbb_debug
            files = tbb_debug.dll
        }
        CONFIG(release, debug|release) {
            libDir = $${TBB_PATH}/build/windows_ia32_gcc_mingw_release
            libs *= -L$${libDir} -ltbb
            files = tbb.dll
        }
        includes *= $${TBB_PATH}/include/tbb
        qmake_post_link += $$generateCopyDllCommand($$files, $$libDir, $$dstDir)
    }

    message(qmake_post_link $${qmake_post_link})

    !exists($${libDir}){
        warning(Could not find your compiled TBB dll)
    }

    #message(----------------Includepath is $${includes})
    #message(----------------Libs is $${libs})
    return($${includes} $${libs} $${qmake_post_link})
}
