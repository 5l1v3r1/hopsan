include( ../Common.prf )

#Helpfunction to fetch correct QWT INCLUDEPATH and LIBS path
defineReplace(setQWTPathInfo){
    #Assign input arguments
    externalSrc = $$1
    dstDir = $$2

    #Set QWT paths, Paths that are higher up in the list will have priority if found
    contains($$QMAKE_HOST.arch, x86_64){
        # 64 bit release
        QWT_PATHS *= $${PWD}/Dependencies/qwt-svn_64
        QWT_PATHS *= $${PWD}/Dependencies/qwt-6.1.0_64
    } else {
        # 32 bit versions
        QWT_PATHS *= $${PWD}/Dependencies/qwt-svn
        QWT_PATHS *= $${PWD}/Dependencies/qwt-6.1.0
    }
    QWT_PATH = $$selectPath($$externalSrc, $$QWT_PATHS, "qwt")

    #message(QWT_PATH $${QWT_PATH})

    #Empty variables to fill in
    magic_hopsan_libpath =
    magic_hopsan_includepath =
    magic_hopsan_qmake_post_link =
    libDir =

    exists($${QWT_PATH}){
        libDir = $${QWT_PATH}/lib
        #message(libDir: $${libDir})

        #Check libfolder path
        exists($${libDir}){
            files =
            libName = qwt

            #Add debug if debugmode (only default on for windows)
            dbg_ext =
            win32:CONFIG(debug, debug|release):dbg_ext = d
            #unix:CONFIG(debug, debug|release):dbg_ext = d

            magic_hopsan_libpath = -L$${libDir} -l$${libName}$${dbg_ext}
            magic_hopsan_includepath = $${QWT_PATH}/src

            #Set files to copy,  only qwt[d].dll/libqwt[d].so
            win32:files = $${libName}$${dbg_ext}$${DLL_EXT}
            unix:files = lib$${libName}$${dbg_ext}$${DLL_EXT}*

            #Generate the copy command
            magic_hopsan_qmake_post_link = $$generateCopyDllCommand($$files, $$libDir, $$dstDir)
            #message(qmake_post_link: $${magic_hopsan_qmake_post_link})
        }
    }

    !exists($${libDir}){
        warning('Could not find your compiled qwt dll')
        magic_hopsan_libpath =
        magic_hopsan_includepath =
        magic_hopsan_qmake_post_link =
    }

    #Export lists to INCLUDEPATH, LIBS and QMAKE_POST_LINK variables
    export(magic_hopsan_libpath)
    export(magic_hopsan_includepath)
    export(magic_hopsan_qmake_post_link)

    #message(----------------Includepath is $${magic_hopsan_includepath})
    #message(----------------Libs is $${magic_hopsan_libpath})

    return($$magic_hopsan_libpath)
}

#Helpfunction to fetch correct PythonQt INCLUDEPATH and LIBS path
defineReplace(setPythonQtPathInfo){
    #Assign input arguments
    externalSrc = $$1
    dstDir = $$2

    #Set QWT paths, Paths that are earlier in the list will be used if found
    contains($$QMAKE_HOST.arch, x86_64){
        # 64 bit version
        PYTHONQT_PATHS *= $${PWD}/Dependencies/PythonQt2.1_Qt4.8_64
    } else {
        # 32 bit version
        PYTHONQT_PATHS *= $${PWD}/Dependencies/PythonQt2.1_Qt4.8
    }
    PYTHONQT_PATH = $$selectPath($$externalSrc, $$PYTHONQT_PATHS, "pythonqt")

    #Empty variables to fill in
    magic_hopsan_libpath =
    magic_hopsan_includepath =
    magic_hopsan_qmake_post_link =
    libDir =

    exists($${PYTHONQT_PATH}){
        libDir = $${PYTHONQT_PATH}/lib
        #Check libfolder path
        exists($${libDir}){
            files =

            magic_hopsan_libpath = -L$${libDir} -lPythonQt$${DEBUG_EXT} -lPythonQt_QtAll$${DEBUG_EXT}
            magic_hopsan_includepath = $${PYTHONQT_PATH}/src $${PYTHONQT_PATH}/extensions/PythonQt_QtAll

            #Set files to copy
            win32:files = PythonQt$${DEBUG_EXT}$${DLL_EXT} PythonQt_QtAll$${DEBUG_EXT}$${DLL_EXT}
            unix:files = libPythonQt$${DEBUG_EXT}$${DLL_EXT}* libPythonQt_QtAll$${DEBUG_EXT}$${DLL_EXT}*

            #Generate the copy command
            magic_hopsan_qmake_post_link = $$generateCopyDllCommand($$files, $$libDir, $$dstDir)
            #message(qmake_post_link $${magic_hopsan_qmake_post_link})
        }
    }

    !exists($${libDir}){
        warning('Could not find your compiled PythonQt dll dir')
        magic_hopsan_libpath =
        magic_hopsan_includepath =
        magic_hopsan_qmake_post_link =
    }

    #Export lists to INCLUDEPATH, LIBS and QMAKE_POST_LINK variables
    export(magic_hopsan_libpath)
    export(magic_hopsan_includepath)
    export(magic_hopsan_qmake_post_link)

    #message(----------------Includepath is $${magic_hopsan_includepath})
    #message(----------------Libs is $${magic_hopsan_libpath})

    return($$magic_hopsan_libpath)
}
